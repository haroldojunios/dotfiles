## Set values
# Hide welcome message
set fish_greeting
set -x PAGER "bat --plain"
set -x MANPAGER "sh -c 'col -bx | bat -l man -p'"
set -x MICRO_TRUECOLOR 1
set -U fish_features qmark-noglob
set -x WINIT_X11_SCALE_FACTOR 1
set -x npm_config_prefix "$HOME/.local"
set -x BAT_STYLE 'numbers,changes,grid,header-filename,header-filesize'
set -x BAT_THEME 'Catppuccin Mocha'
set -x RIPGREP_CONFIG_PATH ~/.ripgreprc

# nnn
set BLK 03
set CHR 03
set DIR 04
set EXE 02
set REG 07
set HARDLINK 05
set SYMLINK 05
set MISSING 08
set ORPHAN 01
set FIFO 06
set SOCK 03
set UNKNOWN 01
set -x NNN_COLORS "#04020301;4231"
set -x NNN_FCOLORS "$BLK$CHR$DIR$EXE$REG$HARDLINK$SYMLINK$MISSING$ORPHAN$FIFO$SOCK$UNKNOWN"
set -x NNN_PLUG 'f:finder;o:fzopen;p:preview-tui;d:diffs'
set -x NNN_PREVIEWDIR "$HOME/.cache/nnn/previews"
set -x NNN_BATTHEME $BAT_THEME
set -x NNN_BATSTYLE $BAT_STYLE

if set -q KITTY_WINDOW_ID && type -q kitten
    alias ssh="kitten ssh"
end

{{ if eq .osid "android" }}
set -x USER haroldo

if not pgrep -f crond >/dev/null && type -q crond
    crond
end

if not pgrep -f sshd >/dev/null && type -q sshd
    sshd
end

{{   if .installDE }}
set -x DISPLAY ":1"
{{   end }}
{{ else }}
if not test -f ~/.cache/weather && type -q get-weather
    get-weather ~/.cache/weather &>/dev/null &
    disown
end
test -f /usr/share/xdg-desktop-portal/portals/kde.portal && set -x GTK_USE_PORTAL 1
{{ end }}


if test "$TERM" = linux
    if type -q yaft
        set -x FRAMEBUFFER /dev/fb0
        # set -x YAFT wall
        yaft
    end
end

## Export variable need for qt-theme
if type -q qtile
    set -x QT_QPA_PLATFORMTHEME qt5ct
end

# Set settings for https://github.com/franciscolourenco/done
set -x __done_min_cmd_duration 10000
set -x __done_notification_urgency_level low
set -x __done_notification_urgency_level_failure normal

## Environment setup
# Apply .profile: use this to put fish compatible .profile stuff in
if test -f ~/.fish_profile
    source ~/.fish_profile
end

if test -f ~/.aliases
    source ~/.aliases
end

# Add ~/bin to PATH
if test -d ~/bin
    if not contains -- ~/bin $PATH
        set -pgx PATH ~/bin
    end
end

# Add ~/.local/bin to PATH
if test -d ~/.local/bin
    if not contains -- ~/.local/bin $PATH
        set -pgx PATH ~/.local/bin
    end
end

# Add ~/.local/bin/lastools/bin to PATH
if test -d ~/.local/bin/lastools/bin
    if not contains -- ~/.local/bin/lastools/bin $PATH
        set -pgx PATH ~/.local/bin/lastools/bin
    end
end

# Add /usr/local/texlive/bin/x86_64-linux to PATH
if test -d /usr/local/texlive/bin/x86_64-linux
    if not contains -- /usr/local/texlive/bin/x86_64-linux $PATH
        set -pgx PATH /usr/local/texlive/bin/x86_64-linux
    end
end

## Advanced command-not-found hook
if test -f /usr/share/doc/find-the-command/ftc.fish
    source /usr/share/doc/find-the-command/ftc.fish
end

if test -f ~/.config/rclone/rclone.fish
    source ~/.config/rclone/rclone.fish
end

## Functions
# use bat to color --help messages
function h --argument cmd
    $cmd --help 2>&1 | bat --plain --language=help
end

function fish_user_key_bindings
    # ctrl-del
    bind \e\[3\;5~ kill-word

    # ctrl-] (backspace)
    bind \cH backward-kill-word

    # ctrl-u
    bind \cu kill-whole-line
end

# Functions needed for !! and !$ https://github.com/oh-my-fish/plugin-bang-bang
function __history_previous_command
    switch (commandline -t)
        case "!"
            commandline -t $history[1]
            commandline -f repaint
        case "*"
            commandline -i !
    end
end

function __history_previous_command_arguments
    switch (commandline -t)
        case "!"
            commandline -t ""
            commandline -f history-token-search-backward
        case "*"
            commandline -i '$'
    end
end

if [ "$fish_key_bindings" = fish_vi_key_bindings ]
    bind -Minsert ! __history_previous_command
    bind -Minsert '$' __history_previous_command_arguments
else
    bind ! __history_previous_command
    bind '$' __history_previous_command_arguments
end

# Fish command history
function history
    builtin history --show-time='%F %T '
end

{{ template "shell/colors.fish" . }}

{{ if .isWSL }}
if service --status-all 2>/dev/null | grep docker | grep - &>/dev/null
    sudo service docker start &>/dev/null
end
{{ end }}

## Run fastfetch if session is interactive
if status --is-interactive && test -z "$VSCODE" && begin
        test -z "$TMUX" || test $(tmux display -p '#{pane_index}') -eq 1
    end
    if type -q fastfetch
        fastfetch --load-config neofetch
    else if type -q neofetch
        neofetch
    else if type -q wslfetch
        wslfetch
    end
end

type -q nvim && set -x EDITOR nvim || set -x EDITOR vim

if test -f ~/.config/.env
    while read -la line
        set -x $(string split -m1 '=' "$line")
    end <~/.config/.env
end

if test -f ~/.config/.env.local
    while read -la line
        set -x $(string split -m1 '=' "$line")
    end <~/.config/.env.local
end

# fish theme
if test -f ~/.config/fish/themes/catppuccin.theme
    fish_config theme choose catppuccin
end

# lazygit theme
if type -q lazygit
    set -l lazygit_config_dir "$(lazygit --print-config-dir)"
    set -l lazygit_config_path "$lazygit_config_dir/config.yml"
    set -l lazygit_theme_path "$lazygit_config_dir/catppuccin.yml"
    set -x LG_CONFIG_FILE "$lazygit_config_path,$lazygit_theme_path"
end

## Mcfly history
if status --is-interactive && type -q mcfly
    mcfly init fish | source
end

## zoxide
if status --is-interactive && type -q zoxide
    zoxide init fish | source
end

## pyenv
if status --is-interactive && type -q pyenv
    set -x PYENV_ROOT $HOME/.pyenv
    set fish_user_paths $PYENV_ROOT/bin $fish_user_paths
    pyenv init - | source
end

# stderred
if test -f /usr/lib/libstderred.so
    set -x LD_PRELOAD /usr/lib/libstderred.so
end

## Starship prompt
{{- if eq .osid "windows" }}
if type -q starship
    starship init fish | source
end
{{- else }}
if status --is-interactive && type -q starship
    starship init fish | source
end
{{- end }}
